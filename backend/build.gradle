plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.profroid'
version = '0.0.1-SNAPSHOT'
description = 'profroidApp'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
ext {
    mapstructVersion = "1.6.3"
    lombokVersion = "1.18.36"
    lombokMapstructBindingVersion = "0.2.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation 'org.springframework.boot:spring-boot-starter-security'
    
    // JWT Token handling
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    runtimeOnly "com.mysql:mysql-connector-j"
    runtimeOnly "com.h2database:h2"
    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    // Test dependencies
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    // For WebTestClient in integration tests
    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    // Test annotation processors
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    
}

tasks.jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/utils/**",
                    "**/config/**",
                    "**/mappingLayer/**",
                    "**/ProfroidAppApplication.class",
                    '**/auth/**',
                    "**/appointmentsubdomain/businessLayer/**",
                    "**/employeesubdomain/businessLayer/employeeScheduleBusinessLayer/**",

            ])
        }))
    }

    reports {
        xml.required = true
        html.required = true
    }
}

tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
